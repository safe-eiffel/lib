indexing
	description: "Objects that represent the Vision2 application.%
		%The original version of this class has been generated by EiffelBuild."
	date: "$Date$"
	revision: "$Revision$"

class
	GUISQL

inherit
	EV_APPLICATION

	ISQL
		undefine
			default_create,
			copy, is_equal,
			create_current_context
		redefine
			current_context, do_session, create_initial_context
		end

create
	make_and_launch

feature {NONE} -- Initialization

	make_and_launch is
			-- Create `Current', build and display `main_window',
			-- then launch the application.
		do
			default_create
			create main_window
			main_window.show
			make
		end

feature -- Access

	current_context : GUISQL_CONTEXT

feature {NONE} -- Implementation

	main_window: MAIN_WINDOW
		-- Main window of `Current'.

	create_current_context (a_output_file: KI_TEXT_OUTPUT_STREAM; a_commands: ISQL_COMMANDS) is
		do
			create current_context.make_gui (main_window.sql_data, main_window.messages, main_window.status_message, a_output_file, a_commands)
		end

	do_session is
		do
			launch
		end

	create_initial_context is
		do
			precursor
			main_window.execute_button.select_actions.extend (agent current_context.execute_text (main_window.sql_text))
			main_window.set_key_press_agent (agent current_context.execute_text (main_window.sql_text))
		end

end
